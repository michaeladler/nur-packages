{
  description = "My NUR";

  #inputs.nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
  inputs = {
    nixpkgs.url = "github:michaeladler/nixpkgs/nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    nur.url = "github:nix-community/NUR";
    crane = {
      url = "github:ipetkov/crane";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = { self, nixpkgs, flake-utils, nur, crane }:

    {
      overlay = import ./overlay.nix;
    } // (flake-utils.lib.eachSystem [ "x86_64-linux" ] (system:

      let
        craneOverlay = final: prev: {
          craneLib = crane.lib.${system};
        };
        pkgs = import nixpkgs {
          inherit system;
          overlays = [ craneOverlay self.overlay ];
          config.allowUnfree = true;
        };
        nurPkgs = import nixpkgs {
          inherit system;
          overlays = [ nur.overlay ];
        };

      in
      {

        devShell = with pkgs;
          mkShell {
            nativeBuildInputs = [ update-nix-fetchgit ];
          };

        # we have to use legacyPackages for sets of derivations (trees)
        # the package names generated by flattenTree cause issues when using them,
        # so they are re-exported here
        # https://github.com/numtide/flake-utils/pull/29#issuecomment-817652939
        legacyPackages = {
          inherit (pkgs)
            firefoxAddons
            extraLuaJITPackages
            ;
          linux = {
            zen = pkgs.linuxPackages_zen.kernel;
            lqx = pkgs.linuxPackages_lqx.kernel;
          };
          nvidia = {
            zen = pkgs.linuxPackages_zen.nvidiaPackages.stable;
            lqx = pkgs.linuxPackages_lqx.nvidiaPackages.stable;
          };
          cpupower = {
            zen = pkgs.linuxPackages_zen.cpupower;
            lqx = pkgs.linuxPackages_lqx.cpupower;
          };
        };

        packages = flake-utils.lib.flattenTree {
          inherit (pkgs)
            aoc-cli
            awesome-git
            brave
            btrfs-du
            btrfs-list
            cargo-llvm-cov
            ccls
            chromium-browserpass
            chyle
            cinclude2dot
            clang-format-diff
            criterion
            dotdrop
            efibootguard
            firefox-bin
            fmt_8
            fmtlog
            fstabfmt
            fzy-lua-native
            git-hours
            go-crx3
            go-mod-outdated
            handlr
            kas-container
            leakdice
            libubootenv
            luaprompt
            neovim-unwrapped
            nix-direnv
            pandoc
            qmarkdown
            sbkeys
            sscep
            sumneko-lua-language-server
            vcalendar-filter
            zig-master
            zinit
            zls
            zoxide
            thinkfan
            czmod

            # re-exported to benefit from caching
            fwupd
            bear
            ;
        };

        defaultPackage = pkgs.hello;

      }));
}
