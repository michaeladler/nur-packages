{
  description = "My NUR";

  inputs.nixpkgs.url = "github:nixos/nixpkgs/nixpkgs-unstable";
  inputs.flake-utils.url = "github:numtide/flake-utils";

  inputs.rnix-lsp-src = {
    url = "github:nix-community/rnix-lsp";
    inputs.nixpkgs.follows = "nixpkgs";
  };

  inputs.nur.url = github:nix-community/NUR;

  outputs = { self, nixpkgs, flake-utils, rnix-lsp-src, nur }:

    {
      overlay = import ./overlay.nix;
    } // (flake-utils.lib.eachSystem [ "x86_64-linux" ] (system:

      let
        pkgs = import nixpkgs {
          inherit system;
          overlays = [ self.overlay ];
          config.allowUnfree = true;
        };
        nurPkgs = import nixpkgs {
          inherit system;
          overlays = [ nur.overlay ];
        };

      in
      {

        # we have to use legacyPackages for sets of derivations (trees)
        # the package names generated by flattenTree cause issues when using them,
        # so they are re-exported here
        # https://github.com/numtide/flake-utils/pull/29#issuecomment-817652939
        legacyPackages = {
          vimPlugins = pkgs.vimPlugins;
          tmuxPlugins = pkgs.tmuxPlugins;
          firefoxAddons = pkgs.firefoxAddons;
        };

        packages = flake-utils.lib.flattenTree {
          inherit (rnix-lsp-src.packages.x86_64-linux) rnix-lsp;
          inherit (pkgs)
            brave
            btrfs-du
            btrfs-list
            cpio
            crx3
            dwm
            dwmblocks
            firefox-bin
            fstabfmt
            goofys
            luaprompt
            minigbm
            msktutil
            myTmuxPlugins
            myVimPlugins
            nix-direnv
            oelint-adv
            peda
            qmarkdown
            shell-snoop
            sscep
            ungoogled-chromium-bin
            vcalendar-filter
            xplugd
            zig
            zls
            zsh-autosuggestions
            zsh-fast-syntax-highlighting
            zsh-pandoc-completion
            zsh-vi-mode
            ;
        };

        defaultPackage = pkgs.hello;

        apps.firefox-addons-generator = flake-utils.lib.mkApp { drv = nurPkgs.nur.repos.rycee.firefox-addons-generator; };

      }));
}
