{
  description = "My NUR";

  inputs.nixpkgs.url = "github:nixos/nixpkgs/nixpkgs-unstable";
  inputs.flake-utils.url = "github:numtide/flake-utils";

  inputs.zls-source = {
    url = "https://github.com/zigtools/zls.git";
    type = "git";
    flake = false;
    submodules = true;
  };

  outputs = { self, nixpkgs, flake-utils, zls-source }:
    flake-utils.lib.eachSystem [ "x86_64-linux" ] (system:
      let
        pkgs = nixpkgs.legacyPackages.${system};
        sources = import ./nix/sources.nix;
        callPackage = nixpkgs.legacyPackages.${system}.callPackage;
        recurseIntoAttrs = nixpkgs.legacyPackages.${system}.recurseIntoAttrs;

        vim-plugins-custom =
          recurseIntoAttrs (callPackage ./pkgs/vim-plugins-custom { });
        tmux-plugins-custom = callPackage ./pkgs/tmux-plugins-custom { };
      in rec {

        # we have to use legacyPackages for sets of derivations (trees)
        # the package names generated by flattenTree cause issues when using them,
        # so they are re-exported here
        # https://github.com/numtide/flake-utils/pull/29#issuecomment-817652939
        legacyPackages = { inherit vim-plugins-custom tmux-plugins-custom; };

        packages = flake-utils.lib.flattenTree rec {
          inherit vim-plugins-custom tmux-plugins-custom;

          afew = callPackage ./pkgs/afew { };
          cpio = callPackage ./pkgs/cpio { };
          dwm = callPackage ./pkgs/dwm { inherit sources; };
          dwmblocks = callPackage ./pkgs/dwmblocks { inherit sources; };
          luaprompt = callPackage ./pkgs/luaprompt { inherit sources; };
          neomutt-nightly =
            callPackage ./pkgs/neomutt-nightly { inherit sources; };
          neovim-nightly-unwrapped =
            callPackage ./pkgs/neovim-nightly/unwrapped.nix {
              inherit sources;
            };
          oelint-adv = callPackage ./pkgs/oelint-adv { inherit sources; };
          python3-gbp = callPackage ./pkgs/python3-gbp { inherit sources; };
          st = callPackage ./pkgs/st { inherit sources; };
          vcalendar-filter =
            callPackage ./pkgs/vcalendar-filter { inherit sources; };
          zig-nightly = callPackage ./pkgs/zig-nightly { };
          zls = callPackage ./pkgs/zls {
            src = zls-source;
            zig = zig-nightly;
          };
          zzz = callPackage ./pkgs/zzz { };
        };

        defaultPackage = packages.luaprompt;

      });
}
